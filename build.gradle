plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco' // Jacoco -> codecov 를 위해 추가
}

group = 'team03'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

jacoco {
    toolVersion = "0.8.11" // jacoco 버전 명시
}

ext {
    queryDslVersion = "5.1.0"
    awsSdkVersion = "2.20.160" // 안정적인 버전 사용
    opensearchVersion = "2.10.0"
}

dependencies {
    // AWS SDK BOM 추가로 버전 충돌 방지
    implementation platform("software.amazon.awssdk:bom:$awsSdkVersion")

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Lombok
    implementation 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // OAuth2
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Security
    implementation 'org.springframework.security:spring-security-messaging'

    // AI - Stanford CoreNLP with JAXB exclusions
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation('edu.stanford.nlp:stanford-corenlp:4.5.4') {
        exclude group: 'javax.xml.bind', module: 'jaxb-api'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-impl'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-core'
    }
    implementation('edu.stanford.nlp:stanford-corenlp:4.5.4:models') {
        exclude group: 'javax.xml.bind', module: 'jaxb-api'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-impl'
        exclude group: 'com.sun.xml.bind', module: 'jaxb-core'
    }
    implementation 'com.github.shin285:KOMORAN:3.3.9'

    // OpenSearch Client (Java 17 호환 버전)
    // Spring Data OpenSearch - Java 17과 Spring Boot 3.x 호환
    implementation 'org.opensearch.client:spring-data-opensearch-starter:1.8.1'

    // OpenSearch Java Client - Java 17 호환 버전
    implementation 'org.opensearch.client:opensearch-java:2.10.0'

    // OpenSearch REST Client
    implementation 'org.opensearch.client:opensearch-rest-client:2.10.0'

    // AWS SDK v2 dependencies
    implementation "software.amazon.awssdk:auth:$awsSdkVersion"
    implementation "software.amazon.awssdk:regions:$awsSdkVersion"
    implementation "software.amazon.awssdk:url-connection-client:$awsSdkVersion" // Explicitly keeping this one

    // Jackson for JSON processing
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.+'

    // Jakarta JAXB - 최신 버전 명시적 추가
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.3'

    // Monitoring
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8'

    // AWS S3 - Exclude apache-client to avoid conflict
    implementation('software.amazon.awssdk:s3') {
        exclude group: 'software.amazon.awssdk', module: 'apache-client'
    }

    // HTTP Client - This is a general Apache HTTP Client, not the AWS SDK specific one.
    // It's generally fine to keep this if other parts of your app need it.
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    // Database
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'

    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // kafka
    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework:spring-websocket'
    testImplementation 'org.springframework:spring-messaging'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    finalizedBy(tasks.jacocoTestReport)
    useJUnitPlatform()
}

tasks.named('jacocoTestReport') {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

afterEvaluate {
    tasks.jacocoTestReport {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/MoplApplication.class',
                    '**/Q*',
                    '**/*$*.class',
                    '**/*$*$*.class',
                    '**/team03/mopl/domain/**/dto/**',
                    '**/team03/mopl/domain/**/batch/**',
                    '**/S3Service.class'
            ])
        })
    }
}

// JVM 메모리 설정 제거 (gradle.properties에서 관리)
// tasks.withType(JavaExec) {
//     jvmArgs = ['-Xmx4g', '-Xms1g']
// }

// 의존성 충돌 방지 설정
configurations {
    all {
        // Elasticsearch 클라이언트와의 충돌 방지
        exclude group: 'org.elasticsearch.client'
        exclude group: 'co.elastic.clients'

        // OpenSearch RestHighLevelClient 제외 (deprecated)
        exclude group: 'org.opensearch.client', module: 'opensearch-rest-high-level-client'
        exclude group: 'org.opensearch.client', module: 'opensearch-rest-client-sniffer'
        // Ensure no apache-client is brought in transitively by other dependencies if not explicitly needed
        // exclude group: 'software.amazon.awssdk', module: 'apache-client' // This is a more aggressive exclusion if the above doesn't work
    }
}
